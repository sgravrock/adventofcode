#!/usr/bin/awk -f

BEGIN {
	count = 0
}

/^turn on / {
	split($3, topleft, ",")
	split($5, bottomright, ",")
	manipulate(topleft, bottomright, "on")
}

/^turn off / {
	split($3, topleft, ",")
	split($5, bottomright, ",")
	manipulate(topleft, bottomright, "off")
}

/^toggle / {
	split($2, topleft, ",")
	split($4, bottomright, ",")
	manipulate(topleft, bottomright, "toggle")
}

END {
	print count
}

function manipulate(topleft, bottomright, op) {
	for (x = topleft[1]; x <= bottomright[1]; x++) {
		for (y = topleft[2]; y <= bottomright[2]; y++) {
			k = x "," y

			if (op == "on" || (op == "toggle" && !lights[k])) {
				newval = 1
			} else  {
				newval = 0
			}

			count += (newval - lights[k])
			lights[k] = newval
		}
	}
}
